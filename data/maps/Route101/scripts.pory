const LOCALID_BIRCH        = 2
const LOCALID_ZIGZAGOON    = 4

mapscripts Route101_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        call(ProfBirch_EventScript_UpdateLocation)
        end
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_ROUTE101_STATE, 0 {
            setflag(FLAG_HIDE_MAP_NAME_POPUP)
            setvar(VAR_ROUTE101_STATE, 1)
            end
        }
    ]
}

script Route101_EventScript_StartBirchRescue {
    lockall
    playbgm(MUS_HELP, TRUE)
    msgbox(format("H-help me!"), MSGBOX_DEFAULT)
    closemessage
    setobjectxy(LOCALID_BIRCH, 0, 15)
    setobjectxy(LOCALID_ZIGZAGOON, 0, 16)
    applymovement(OBJ_EVENT_ID_PLAYER, Route101_Movement_EnterScene)
    applymovement(LOCALID_BIRCH, Route101_Movement_BirchRunAway1)
    applymovement(LOCALID_ZIGZAGOON, Route101_Movement_ZigzagoonChase1)
    waitmovement(0)
    applymovement(LOCALID_ZIGZAGOON, Route101_Movement_ZigzagoonChaseInCircles)
    applymovement(LOCALID_BIRCH, Route101_Movement_BirchRunInCircles)
    waitmovement(0)
    applymovement(LOCALID_BIRCH, Common_Movement_WalkInPlaceFasterRight)
    waitmovement(0)
    applymovement(LOCALID_ZIGZAGOON, Route101_Movement_ZigzagoonFaceBirch)
    applymovement(LOCALID_BIRCH, Route101_Movement_BirchFaceZigzagoon)
    waitmovement(0)
    msgbox(format("Hello! You over there!\N"
            "Please! Help!\p"
            "In my BAG!\N"
            "There's a POKé BALL!"), MSGBOX_DEFAULT)
    closemessage
    setvar(VAR_ROUTE101_STATE, 2)
    releaseall
    end
}

script Route101_EventScript_PreventExitSouth {
    lockall
    msgbox(Route101_Text_DontLeaveMe, MSGBOX_DEFAULT)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Route101_Movement_PreventExitSouth)
    waitmovement(0)
    releaseall
    end
}

script Route101_EventScript_PreventExitWest {
    lockall
    msgbox(Route101_Text_DontLeaveMe, MSGBOX_DEFAULT)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Route101_Movement_PreventExitWest)
    waitmovement(0)
    releaseall
    end
}

script Route101_EventScript_PreventExitNorth {
    lockall
    msgbox(Route101_Text_DontLeaveMe, MSGBOX_DEFAULT)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Route101_Movement_PreventExitNorth)
    waitmovement(0)
    releaseall
    end
}

movement Route101_Movement_PreventExitSouth {
    walk_up
}

movement Route101_Movement_PreventExitWest {
    walk_right
}

movement Route101_Movement_PreventExitNorth {
    walk_down
}

movement Route101_Movement_ZigzagoonChaseInCircles {
    walk_fast_up * 3
    walk_fast_right * 3
    walk_fast_down * 2
    walk_fast_left * 3
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_down
    walk_fast_down
    walk_fast_left * 3
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_down * 2
    walk_fast_left * 2
}

movement Route101_Movement_ZigzagoonChase1 {
    walk_fast_up
    walk_fast_right * 4
    walk_fast_up
}

movement Route101_Movement_ZigzagoonFaceBirch {
    walk_in_place_fast_left * 4
}

movement Route101_Movement_EnterScene {
    walk_fast_up * 4
    walk_in_place_faster_left
}

movement Route101_Movement_BirchRunInCircles {
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_down * 2
    walk_fast_left * 3
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_down * 2
    walk_fast_left * 3
    walk_fast_up * 2
    walk_fast_right * 3
    walk_fast_down * 2
    walk_fast_left * 3
}

movement Route101_Movement_BirchRunAway1 {
    walk_fast_right * 4
    walk_fast_up * 2
}

movement Route101_Movement_BirchFaceZigzagoon {
    walk_in_place_fast_right * 4
}

script Route101_EventScript_Youngster {
    msgbox(format("If POKéMON get tired, take them to a POKéMON CENTER.\p"
                "There's a POKéMON CENTER in OLDALE TOWN right close by."), MSGBOX_NPC)
    end
}

script Route101_EventScript_Boy {
    msgbox(format("Wild POKéMON will jump out at you in tall grass.\p"
                    "If you want to catch POKéMON, you have to go into the tall grass and search."), MSGBOX_NPC)
    end
}

script Route101_EventScript_RouteSign {
    msgbox(format("ROUTE 101\N{UP_ARROW} OLDALE TOWN"), MSGBOX_SIGN)
    end
}

script Route101_EventScript_BirchsBag {
    lock
    faceplayer
    setflag(FLAG_SYS_POKEMON_GET)
    setflag(FLAG_RESCUED_BIRCH)
    fadescreen(FADE_TO_BLACK)
    removeobject(LOCALID_ZIGZAGOON)
    setobjectxy(OBJ_EVENT_ID_PLAYER, 6, 13)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
    waitmovement(0)
    special(ChooseStarter)
    waitstate
    applymovement(LOCALID_BIRCH, Route101_Movement_BirchApproachPlayer)
    waitmovement(0)
    msgbox(format("PROF. BIRCH: Whew…\p"
                "I was in the tall grass studying wild POKéMON when I was jumped.\p"
                "You saved me.\N"
                "Thanks a lot!\p"
                "Oh?\p"
                "Hi, you're {PLAYER}{KUN}!\p"
                "This is not the place to chat, so come by my POKéMON LAB later, okay?"), MSGBOX_DEFAULT)
    special(HealPlayerParty)
    setflag(FLAG_HIDE_ROUTE_101_BIRCH_ZIGZAGOON_BATTLE)
    clearflag(FLAG_HIDE_LITTLEROOT_TOWN_BIRCHS_LAB_BIRCH)
    setflag(FLAG_HIDE_ROUTE_101_BIRCH_STARTERS_BAG)
    setvar(VAR_BIRCH_LAB_STATE, 2)
    setvar(VAR_ROUTE101_STATE, 3)
    clearflag(FLAG_HIDE_MAP_NAME_POPUP)
    checkplayergender
    if(var(VAR_RESULT) == MALE)
    {
        setflag(FLAG_HIDE_LITTLEROOT_TOWN_MAYS_HOUSE_RIVAL_BEDROOM)
    }
    else
    {
        setflag(FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_RIVAL_BEDROOM)
    }
    warp(MAP_LITTLEROOT_TOWN_PROFESSOR_BIRCHS_LAB, 6, 5)
    waitstate
    release
    end
}

movement Route101_Movement_BirchApproachPlayer {
    walk_right
}

text Route101_Text_DontLeaveMe {
    format("Wh-Where are you going?!\N"
            "Don't leave me like this!")
}
