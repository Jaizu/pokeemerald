const LOCALID_RIVAL = 1

mapscripts LittlerootTown_BrendansHouse_2F_MapScripts {
	MAP_SCRIPT_ON_TRANSITION
	{
		if(var(VAR_LITTLEROOT_RIVAL_STATE) < 2)
		{
			checkplayergender
			if(var(VAR_RESULT) == MALE)
			{
				setvar(VAR_LITTLEROOT_RIVAL_STATE, 2)
			}
		}
		if(var(VAR_LITTLEROOT_RIVAL_STATE) >= 3)
		{
			if(flag(FLAG_MET_RIVAL_LILYCOVE))
			{
				goto(LittlerootTown_BrendansHouse_2F_EventScript_TryUpdateBrendanPos)
			}
			if(var(VAR_BIRCH_LAB_STATE) >= 2)
			{
				return
			}
			goto(LittlerootTown_BrendansHouse_2F_EventScript_TryUpdateBrendanPos)
		}
		if(var(VAR_LITTLEROOT_INTRO_STATE) == 4)
		{
			call(PlayersHouse_2F_EventScript_BlockStairsUntilClockIsSet)
		}
		call(SecretBase_EventScript_SetDecorationFlags)
		setvar(VAR_SECRET_BASE_INITIALIZED, 0)
		end
	}
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE
	[
		VAR_SECRET_BASE_INITIALIZED, 0 {
			checkplayergender
			if(var(VAR_RESULT) == MALE)
			{
				goto(SecretBase_EventScript_InitDecorations)
			}
			end
		}
	]

}

script LittlerootTown_BrendansHouse_2F_EventScript_TryUpdateBrendanPos {
	checkplayergender
	if(var(VAR_RESULT) == MALE)
	{
		return
	}
	if(var(VAR_DEX_UPGRADE_JOHTO_STARTER_STATE) >= 2)
	{
		return
	}
	setobjectxyperm(LOCALID_RIVAL, 0, 2)
	setobjectmovementtype(LOCALID_RIVAL, MOVEMENT_TYPE_FACE_UP)
}


script LittlerootTown_BrendansHouse_2F_EventScript_RivalsPokeBall {
	lockall
	if(var(VAR_LITTLEROOT_RIVAL_STATE) == 2)
	{
		delay(10)
		addobject(LOCALID_RIVAL)
		applymovement(LOCALID_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanEnters)
		waitmovement(0)
		playse(SE_PIN)
		applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
		waitmovement(0)
		applymovement(LOCALID_RIVAL, Common_Movement_Delay48)
		waitmovement(0)
		delay(10)
		playbgm(MUS_ENCOUNTER_BRENDAN, TRUE)
		switch(var(VAR_FACING))
		{
		case DIR_NORTH:
			applymovement(LOCALID_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerNorth)
			waitmovement(0)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
			waitmovement(0)
			msgbox(RivalsHouse_2F_Text_BrendanWhoAreYou, MSGBOX_DEFAULT)
			closemessage
			applymovement(OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanNorth)
			applymovement(LOCALID_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCNorth)
			waitmovement(0)
		case DIR_SOUTH:
			applymovement(LOCALID_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerSouth)
			waitmovement(0)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
			waitmovement(0)
			msgbox(RivalsHouse_2F_Text_BrendanWhoAreYou, MSGBOX_DEFAULT)
			closemessage
			applymovement(OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanSouth)
			applymovement(LOCALID_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCSouth)
			waitmovement(0)
		case DIR_WEST:
			applymovement(LOCALID_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerWest)
			waitmovement(0)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
			waitmovement(0)
			msgbox(RivalsHouse_2F_Text_BrendanWhoAreYou, MSGBOX_DEFAULT)
			closemessage
			applymovement(OBJ_EVENT_ID_PLAYER, LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanWest)
			applymovement(LOCALID_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCWest)
			waitmovement(0)
		case DIR_EAST:
			applymovement(LOCALID_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerEast)
			waitmovement(0)
			applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFasterUp)
			waitmovement(0)
			msgbox(RivalsHouse_2F_Text_BrendanWhoAreYou, MSGBOX_DEFAULT)
			closemessage
			applymovement(LOCALID_RIVAL, LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCEast)
			waitmovement(0)
		}
		setvar(VAR_LITTLEROOT_RIVAL_STATE, 3)
		setflag(FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F_POKE_BALL)
		clearflag(FLAG_HIDE_LITTLEROOT_TOWN_BRENDANS_HOUSE_RIVAL_BEDROOM)
		setvar(VAR_LITTLEROOT_TOWN_STATE, 1)
		savebgm(MUS_DUMMY)
		fadedefaultbgm
		releaseall
		end
	}
	msgbox(RivalsHouse_2F_Text_ItsRivalsPokeBall, MSGBOX_DEFAULT)
	releaseall
	end
}

movement LittlerootTown_BrendansHouse_2F_Movement_BrendanEnters {
	walk_down * 2
	walk_in_place_faster_left
}

movement LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerNorth {
	walk_left * 2
	walk_down * 2
	walk_left
}

movement LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCNorth {
	walk_up * 3
	walk_left * 4
	walk_in_place_faster_up
}

movement LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanNorth {
	delay_16
	walk_in_place_faster_up
	delay_16 * 4
	walk_in_place_faster_left
}

movement LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerSouth {
	walk_left * 3
}

movement LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCSouth {
	walk_up
	walk_left * 4
	walk_in_place_faster_up
}

movement LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanSouth {
	delay_16
	walk_in_place_faster_up
	delay_16 * 2
	walk_in_place_faster_left
}

movement LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerWest {
	walk_left * 2
	walk_down
	walk_in_place_faster_left
}

movement LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCWest {
	walk_up * 2
	walk_left * 5
	walk_in_place_faster_up
}

movement LittlerootTown_BrendansHouse_2F_Movement_PlayerWatchBrendanWest {
	delay_8
	delay_16
	walk_in_place_faster_up
	delay_16 * 2
	walk_in_place_faster_left
}

movement LittlerootTown_BrendansHouse_2F_Movement_BrendanApproachPlayerEast {
	walk_left * 5
	walk_in_place_faster_down
}

movement LittlerootTown_BrendansHouse_2F_Movement_BrendanWalkToPCEast {
	walk_up
	walk_left
	walk_left
	walk_in_place_faster_up
}

script LittlerootTown_BrendansHouse_2F_EventScript_PC {
	lockall
	checkplayergender
	if(var(VAR_RESULT) == MALE)
	{
		setvar(VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE)
		special(DoPCTurnOnEffect)
		playse(SE_PC_ON)
		msgbox(gText_PlayerHouseBootPC, MSGBOX_DEFAULT)
		special(BedroomPC)
		waitstate
		releaseall
		end
	}
	elif (var(VAR_RESULT) == FEMALE)
	{
		msgbox(gText_PokemonTrainerSchoolEmail, MSGBOX_DEFAULT)
		releaseall
		end
	}
	end
}

script LittlerootTown_BrendansHouse_2F_EventScript_TurnOffPlayerPC {
	setvar(VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE)
	playse(SE_PC_OFF)
	special(DoPCTurnOffEffect)
	releaseall
	end
}

script PlayersHouse_2F_EventScript_Notebook {
	msgbox("{PLAYER} flipped open the notebook.\p"
			"ADVENTURE RULE NO. 1\n"
			"Open the MENU with START.\p"
			"ADVENTURE RULE NO. 2\n"
			"Record your progress with SAVE.\p"
			"The remaining pages are blank…", MSGBOX_SIGN)
	end
}

script PlayersHouse_2F_EventScript_GameCube {
	msgbox("It's a Nintendo GameCube.\p"
			"A Game Boy Advance is connected to\n"
			"serve as the Controller.", MSGBOX_SIGN)
	end
}

text PlayersHouse_2F_Text_ClockIsStopped {
	"The clock is stopped…\p"
	"Better set it and start it!"
}

text PlayersHouse_2F_Text_HowDoYouLikeYourRoom {
	"MOM: {PLAYER}, how do you like your\n"
	"new room?\p"
	"Good! Everything's put away neatly!\p"
	"They finished moving everything in\n"
	"downstairs, too.\p"
	"POKéMON movers are so convenient!\p"
	"Oh, you should make sure that\n"
	"everything's all there on your desk.$"
}

text Common_Text_LookCloserAtMap {
	"{PLAYER} took a closer look at the\n"
	"HOENN region map."
}
